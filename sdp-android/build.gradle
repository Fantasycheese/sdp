apply plugin: 'com.android.library'
apply plugin: 'maven'
apply plugin: 'signing'

buildscript {
    repositories {
        jcenter()
        maven {
            url 'https://maven.google.com/'
            name 'Google'
        }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.0.1'
    }
}


android {
    compileSdkVersion 28
    buildToolsVersion "28.0.3"

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 28
    }
}

class ScalePixelFactory extends DefaultTask {

    @Input double dpMax
    @Input double dpMin
    @Input double spMax
    @Input double spMin

    @TaskAction
    def create() {
        String resFolder =  project.getProjectDir().getPath() + "/src/main/res/";
        for(int dimen = 300; dimen <= 800 ; dimen = dimen + 5){
            String folder = resFolder + (dimen == 300 ? "values" : ("values-sw" + (int)dimen + "dp"));
            String fileName = folder + "/_scale_pixels.xml";
            new File(folder).mkdir();
            new File(fileName).createNewFile();
            PrintWriter printWriter = new PrintWriter(fileName);
            printWriter.println("<?xml version=\"1.0\" encoding=\"utf-8\"?>");
            printWriter.println("<resources>");

            for(int i = dpMin; i<= dpMax; ++i)
                writeLine(printWriter, i, dimen, "dp");

            for(int i = spMin; i<= spMax; ++i)
                writeLine(printWriter, i, dimen, "sp");

            printWriter.println("</resources>");
            printWriter.close();
        }
    }

    private static void writeLine(PrintWriter printWriter, int i, int dimen, String unit) {
        double ratio = i/1080d;
        double value = ratio*dimen;
        String name = String.format("_%ss%s", (i >= 0 ? "":"minus_")+Math.abs(i).toString(), unit);
        printWriter.printf("\t<dimen name=\"%s\">%.2f%s</dimen>\r\n", name, value, unit);
    }
}

task createScalePixel(type: ScalePixelFactory) {
}

//uncomment next line to edit values
preBuild.dependsOn createScalePixel

createScalePixel {
    dpMax = 1920
    dpMin = -60
    spMax = 200
    spMin = 0
}

signing {
    required { gradle.taskGraph.hasTask("uploadArchives") }
    sign configurations.archives
}

repositories {
    maven {
        url 'https://maven.google.com/'
        name 'Google'
    }
}